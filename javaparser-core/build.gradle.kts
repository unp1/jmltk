plugins {
    id("buildlogic.java-conventions")
    id("publish-convention")
}

description = "io.github.jmltoolkit:jmlparser-core"

val javacc = configurations.create("javacc")

dependencies {
    api(libs.org.jspecify.jspecify)
    api(libs.net.bytebuddy.byte.buddy.agent)
    // This version does not work:
    //javacc("com.helger:parser-generator-cc:2.0.1")
    javacc("com.helger:parser-generator-cc:1.1.4")
}

val buildFile = layout.buildDirectory.dir("generated-src/main/buildfile/")
sourceSets.main.get().java.srcDir(buildFile)

val javaBuildFile = tasks.register<Copy>("javaBuildFile") {
    description = "Create a Java file containing build information"
    from("src/main/java-templates/")
    includeEmptyDirs = false

    into(buildFile)
    expand(
        "name" to project.name,
        "version" to project.version,
        "groupId" to project.group,
        "artifactId" to "jmlparser-core"
    )
}

tasks.compileJava { dependsOn(javaBuildFile) }
tasks.sourcesJar { dependsOn(javaBuildFile) }
tasks.spotlessJava { dependsOn(javaBuildFile); dependsOn(tasks.named("compileJavacc")) }

val javaccOutput: String =
    layout.buildDirectory
        .dir("generated-src/main/javacc")
        .get()
        .asFile.absolutePath

val javaccInput = "src/main/javacc/java.jj"

val compileJavacc = tasks.register<JavaExec>("compileJavacc") {
    description = "Compiles the JavaCC grammars into Java"

    inputs.file(javaccInput).withPathSensitivity(PathSensitivity.RELATIVE)
    outputs.dir(javaccOutput)
    mainClass.set("com.helger.pgcc.parser.Main")
    classpath(javacc)
    args =
        listOf(
            "-OUTPUT_DIRECTORY=$javaccOutput/com/github/javaparser",
            "src/main/javacc/java.jj",
        )
}
tasks.compileJava { dependsOn(compileJavacc) }
tasks.sourcesJar { dependsOn(compileJavacc) }

sourceSets.main {
    java {
        srcDirs(javaccOutput, "src/main/javacc-support")
    }
}

tasks.checkstyleMain {
    //source("src/main/java")
    //exclude("*/build/*")
    exclude("/home/weigl/work/javaparser/javaparser-core/build/generated-src/main/javacc/com/github/javaparser/GeneratedJavaParserTokenManager.java")
}